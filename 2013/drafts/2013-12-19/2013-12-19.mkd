Title:    Qt 사용하기
Package:  Seoul.pm
Category: perl
Category: Seoul.pm
Category: Qt
Category: GUI
Author:   yukinpl

저자
-----

[@yukinpl][twitter-yukinpl] - 아는 것이 아무것도 없는 Perl 초보,
[eriny.net][home-yukinpl]을 운영하고 있다.


<p>
	<b>Linux에서의 GUI / Perl 에서의 GUI</b><br />
	Linux에서 GUI하면 두 가지, GTK와 Qt가 떠오르시나요? 그러나 Perl에서는 Qt가 유독 힘을 쓰지 못하는 것 같습니다. Perl에서 GUI를 표현하기 위한 모듈으로는 wxPerl, Perl-Tk, Perl/Qt or Perl/KDE, gtk2-perl 등이 있는 듯 합니다<sup>[1]</sup>. 그 중에서 Windows 유저들에게는 wxPerl이 환영받는 듯 하고, Linux 유저들에게는 gtk2가 인기 있는 것 같습니다. 대체 왜 그럴까요? 아무래도 Qt는 지속적으로 업데이트 되고 있는 모듈 찾기도 쉽지 않고, 안정화 되어 있어 잘 알려진 모듈도 흔하지 않은 것이 가장 큰 이유인 것 같고 라이센스 문제도 있겠지요(원인을 잘 아신다면 제게 알려주세요!!). 개인적으로는 Linux에서 C++ 프로그램 개발시 GTK보다 Qt로 작업한 것이 더 많은 터라 친근하기도 하고 Perl에서 대우 받지 못하는 것 같아 소개해 보려 합니다.<br />
	&nbsp;<br /></p>

<p>
	<b>사용하려면</b><br />
	Perl에서 Qt를 사용하려면 아래 모듈이 필요합니다.<br />
	<li><a href="http://search.cpan.org/~cburel/Qt-0.96.0">http://search.cpan.org/~cburel/Qt-0.96.0</a></li>
	<br />
	그러나 위 모듈을 사용하려면 아래 준비물들이 우선 필요합니다.<br />
	<li><b>Qt 4</b> - 위 모듈은 Qt 4를 기준으로 작성되어 있습니다.</li>
	<li><b>SmokeQt</b> - Scipting Meta Object Kcompiler의 약자로 KDE의 binding 모듈입니다.</li>
	<li><b>cmake</b> - Makefile.PL이 cmake를 기반으로 동작합니다.</li>
	&nbsp;<br /></p>

<p>
	<b>설치하자!</b><br />
	이제 설치해보겠습니다. 아래 내용은 Ubuntu 12.04 LTS를 기준으로 진행하겠습니다.<br />
	우선 해당 모듈 설치를 위해 필요한 도구들을 먼저 설치하겠습니다.<br />
	<pre class="code">
$ sudo apt-get install cmake
$ sudo apt-get install libqt4-core libqt4-dbg libqt4-dev libqt4-gui
$ sudo apt-get install SmokeQt*</pre>
	컴파일 도구가 준비되어 있지 않다면 아래의 도구들도 함께 설치하겠습니다.<br />
	<pre class="code">
$ sudo apt-get install build-essentials
$ sudo apt-get install gcc
$ sudo apt-get install g++</pre>
	여기까지 문제 없이 설치되었다면 이제 모듈을 설치해보겠습니다.<br />
	<pre class="code">
$ cd ~/Downloads
$ tar zxvf Qt-0.96.0.tar.gz
$ cd Qt-0.96.0.tar.gz
$ ./Makefile.PL</pre>
	Makefile.PL 을 실행시키는 동안 문제가 발생한다면 cmake 또는 SmokeQt가 설치되지 않은 것입니다. 설치과정에서 빠진 것은 아닌지 확인하셔야 합니다.<br />
	여기까지 문제 없이 진행되었다면 이제 설치는 막바지입니다.<br />
	<pre class="code">
$ make
$ sudo make install</pre>
</code>
	&nbsp;<br /></p>

<p>
	<b>약간의 설정 및 수정</b><br />
	설치가 완료되었다면 한가지의 설정과 한가지의 수정사항이 남아 있습니다. 첫번째는 PERL5LIB 환경변수 추가에 대한 부분이며, 두번째는 설치된 모듈 파일의 수정입니다.<br />
	이 부분은 아래 내용을 진행하면서 문제가 발생했을 때 이부분을 참조하셔서 수행하시기 바랍니다.<br />
	&nbsp;<br />
	<b>+ PERL5LIB 설정 추가</b> 아래 예제들을 실행하면서 @INC 오류가 발생하면 지금 설치한 모듈이 설치된 디렉토리를 @INC에 추가하시기 바랍니다.<br />
	<pre class="code">
export PERL5LIB=/home/yukinpl/perllib:/usr/local/lib/perl/5.14.2</pre>
	위 설정은 제가 설정한 것으로 앞 부분은 개인적으로 작성한 모듈들을 저장하는 곳이고 Qt가 설치된 디렉토리는 뒷부분입니다.<br />
	&nbsp;<br />
	<b>+ QtCore4.pm 수정</b> 아래 예제들을 실행하면서 List::MoreUtils 에 대한 @INC를 요구하는 경우 QtCore4.pm 파일을 수정해야합니다. 설치된 위치에 있는 QtCore4.pm 파일을 아래와 같이 수정하시기 바랍니다.<br />
	<pre class="code">
#use List::MoreUtils qw( any );</pre>
	QtCore4.pm 파일의 799 라인으로 이동하면 List::MoreUtils qw(any) 라는 라인이 있습니다. 이 부분을 주석처리 하시기 바랍니다.<br />
	&nbsp;<br /></p>

<p>
	<b>사용해보자</b><br />
	아래 예제를 실행해 보도록 하겠습니다.<br />
	<pre class="code">
#!/usr/bin/env perl
use warnings;
use strict;
use QtCore4;
use QtGui4;
use QtCore4::debug qw(ambiguous);

my $app= Qt::Application(\@ARGV);
my $button= Qt::PushButton('Hello, world!', undef);

$button->resize(200,30);
$button->show();

exit $app->exec();
</pre>
	위 예제 실행시 문제가 없다면 아래와 같이 실행됩니다.<br />
	<img src="http://np.3.eriny.net/eriny.net/files/2013/20131217/Screenshot from 2013-12-17 15:22:35.png"></img><br />
	여기까지 잘 진행되었다면 Qt를 사용하는 것에 무리가 없을 것입니다.<br />
	&nbsp;<br /></p>

<p>
	<b>SIGNAL / SLOT</b><br />
	Qt를 잘 사용하려면 자주 만나게 될 서브루틴이 있습니다.<br />
	바로 <code>Qt::Object::connect()</code> 입니다. 그리고 SIGNAL과 SLOT이라는 개념을 이해해야합니다. Event-driven programming을 잘 이해하고 계시다면 간단합니다. SIGNAL은 이벤트 발생 조건이고 SLOT은 이벤트 처리자라고 생각하시면 됩니다. 그리고 이것을 묶어주는 역활을 하는 서브루틴이 connect()입니다. 아래 예제는 버튼을 클릭하면 프로그램이 종료되는 간단한 예제입니다. 이 예제를 보면 쉽게 이해할 수 있으리라 예상됩니다.<br />
	<pre class="code">
#!/usr/bin/env perl
use warnings;
use strict;
use QtCore4;
use QtGui4;
use QtCore4::debug qw(ambiguous);

main();

sub main {
  my $app= Qt::Application(\@ARGV);
  my $btnQuit= Qt::PushButton("Quit");

  $btnQuit->resize(200, 30);
  Qt::Object::connect($btnQuit, SIGNAL "clicked()", $app, SLOT "quit()");

  $btnQuit->show();
  return $app->exec();
}
</pre>
	위 코드를 보면서 이해하셨겠지만 설명하자면 connect는 이벤트 발생 객체, 이벤트, 이벤트 처리 객체, 이벤트 처리자 순으로 입력하도록 되어 있습니다.<br />
	&nbsp;<br /></p>

<p>
	<b>화면 구성 1</b><br />
	지금까지의 예제는 컨트롤 하나(PushButton)만을 만들었는데 버젓한 화면을 구성하려면 어떻게 해야 할까요? 기본적으로 제공되는 컨트롤들은 parent 컨트롤를 설정할 수 있습니다. 아래 코드에 보시면 btnQt(PushButton)을 window(Widget)에 붙이는 것을 확인하실 수 있습니다.<br />
	<pre class="code">
#!/usr/bin/env perl
use warnings;
use strict;
use QtCore4;
use QtGui4;
use QtCore4::debug qw(ambiguous);

main();

sub main {
  my $app= Qt::Application(\@ARGV);
  my $window= Qt::Widget();

  $window->resize(200, 50);

  my $btnQuit= Qt::PushButton("Quit", $window);
  $btnQuit->setGeometry(10, 10, 180, 30);

  Qt::Object::connect($btnQuit, SIGNAL "clicked()", $app, SLOT "quit()");

  $btnQuit->show();
  return $app->exec();
}
</pre>
	&nbsp;<br /></p>

<p>
	<b>화면 구성2</b><br />
	여기까지 다다르고 나서 무언가 만들려고 하면 어떻게 해야할까? 를 고민하게 하는 문제가 있습니다. Widget에 컨트롤을 붙이기는 했는데 버튼을 눌렀을 때 기존 컨트롤들이 가지고 있는 동작이 아닌 다른 동작을 하려면 어떻게 해야할 까요? 아니면 Widget의 형태를 미리 만들어 둘 수는 없을까요? 이를 위해서는 서브 클래싱 방식을 이해해야합니다. 이는 생각보다 간단합니다.<br />
	<pre class="code">
use QtCore4::isa qw(Qt::Widget);</pre>
	위와 같이 <code>QtCore4::isa</code>를 통하여 서브 클래싱할 객체를 선택합니다. 그 후<br />
	<pre class="code">
sub NEW {
  shift->SUPER::NEW(@_);
}</pre>
	위와 같이 NEW를 재구현합니다. 일단은 이로써 기본은 끝입니다. 서브루틴 NEW를 수정하거나 다른 서브루틴을 추가하면 됩니다. 이때 유의해야하는 것은 기본적은 SIGNAL과 SLOT 이외에 추가하는 것은 아래와 같은 방법으로 명시해줘야합니다.<br />
	<pre class="code">
use QtCore4::slots viewMessage => [];</pre>
	&nbsp;<br />
	이를 이용하여 버튼을 눌렀을 때 'Hello World'를 보여주는 프로그램을 작성해 봅시다.<br />
	<pre class="code">
#!/usr/bin/env perl
use warnings;
use strict;

package ExWidget;
use QtCore4;
use QtGui4;
use QtCore4::debug qw(ambiguous);
use QtCore4::isa qw(Qt::Widget);
use QtCore4::slots viewMessage => [];

sub NEW {
  shift->SUPER::NEW(@_);
}

sub viewMessage {
  my $msgbox= Qt::MessageBox();
  $msgbox->setText("Hello World");
  return $msgbox->exec();
}

package main;
use QtCore4;
use QtGui4;
use QtCore4::debug qw(ambiguous);
use ExWidget;

main();

sub main {
  my $app= Qt::Application(\@ARGV);
  my $window= ExWidget();

  $window->resize(200, 50);

  my $btnQuit= Qt::PushButton("View", $window);
  $btnQuit->setGeometry(10, 10, 180, 30);

  Qt::Object::connect($btnQuit, SIGNAL "clicked()", $window, SLOT "viewMessage()");

  $window->show();
  return $app->exec();
}</pre>
	<img src="http://np.3.eriny.net/eriny.net/files/2013/20131217/Screenshot from 2013-12-18 16:57:20.png"></img><br />
	&nbsp;<br /></p>

<p>
	<b>글을 마무리하면서</b><br/>
	Qt에 대한 모든 내용을 다루기보다는 기본적인 설치와 사용방법에 중점을 두어 글을 작성했습니다. 글을 읽어보셨다면 아시겠지만(물론 이미 알고 계시겠지만) Qt가 그리 어려운 Framework는 아닙니다. 개인적으로는 perl에서 Qt도 많은 사랑을 받았으면 좋겠습니다. <br />
	&nbsp;<br />
	Enjoy your Perl~~~<br />
	&nbsp;<br /></p>

<p class="footnote">
	<b>[1]</b> - <a href="http://perl-begin.org/uses/GUI">http://perl-begin.org/uses/GUI</a><br />
	&nbsp;<br /></p></div>
</div>
</div>


</body>
</html>


[home-yukinpl]:                 http://eriny.net/
[twitter-yukinpl]:              http://twitter.com/#!/yukinpl
